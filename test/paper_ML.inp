TITLE:      GRADED RESPONSE MODEL WITH BECK YOUTH INVENTORY FOR 
            DEPRESSION-ML ESTIMATION

DATA:       FILE IS C:\SEM_IRT\BYI_DEMO.DAT;

VARIABLE:   NAMES ARE SUBJECT COHORT GRADE AGE I1-I20;
            USEVARIABLES ARE I1-I20;
            CATEGORICAL ARE I1-I20;
            MISSING = ;
            IDVARIABLE = SUBJECT;

ANALYSIS:
!CHANGE ESTIMATOR TO MAXIMUM LIKELIHOOD
ESTIMATOR = ML; 
     
MODEL:
!LABELING FACTOR LOADINGS FOR B1-B20 RELATING THEM TO FACTOR F
       F BY I1-I20* (L1-L20);

!LABELING FACTOR VARIANCE AND MEAN, RESPECTIVELY
       F* (FVAR);
       [F] (FMEAN);

!LABELING THREE THRESHOLDS (#CATEGORIES-1) PER ITEM
       [I1$1-I20$1] (T1I1-T1I20); !THRESHOLD 1 FOR ITEMS 1-20
       [I1$2-I20$2] (T2I1-T2I20); !THRESHOLD 2 FOR ITEMS 1-20
       [I1$3-I20$3] (T3I1-T3I20); !THRESHOLD 3 FOR ITEMS 1-20


!USING MODEL CONSTRAINT TO CALCULATE IRT PARAMETERS
MODEL CONSTRAINT:

!SETTING FACTOR VARIANCE AND FACTOR MEAN TO 1 AND ZERO, RESPECTIVELY
!NEEDED FOR MODEL IDENTIFICATION
FVAR=1;
FMEAN=0;

!CREATING NEW IRT PARAMETERS

!DIS=DISCRIMINATION; D=DIFFICULTY; I=ITEM
NEW(DIS1-DIS20 D1I1-D1I20 D2I1-D2I20 D3I1-D3I20);



!DIS1-DIS20 ARE DISCRIMINATIONS FOR ITEMS 1-20
!DISCRIMINATION CALCULATED USING EQUATION 6 WITH THE DO LOOP
!FOR ITEMS 1-20, REPLACE # WITH RESPECTIVE ITEM #
DO(1,20) DIS# = (L#*SQRT(FVAR))/1.7; !EQUATION 6

!D1I1-D1I20, D2I1-D2I20, AND D3I1-D3I20 ARE DIFFICULTIES
!DIFFICULTIES CALCULATED USING EQUATION 8 WITH THE DO LOOP
!FOR ITEMS 1-20, REPLACE # WITH RESPECTIVE ITEM #
DO(1,20) D1I# =  (T1I#-(L#*FMEAN))/(L#*SQRT(FVAR));
DO(1,20) D2I# =  (T2I#-(L#*FMEAN))/(L#*SQRT(FVAR));
DO(1,20) D3I# =  (T3I#-(L#*FMEAN))/(L#*SQRT(FVAR));
